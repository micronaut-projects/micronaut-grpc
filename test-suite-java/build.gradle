plugins {
    id 'java'
    // tag::plugin[]
    id "com.google.protobuf" version "$protobufGradleVersion"
    // end::plugin[]
    id "io.micronaut.application" 
    id "io.micronaut.build.internal.grpc-base"
}

mainClassName = "helloworld.Application"
micronaut {
    version micronautVersion
    testRuntime "junit5"
    enableNativeImage false
    processing {
        incremental(true)
        annotations("helloworld.*")
    }
}

dependencies {

    annotationProcessor "io.micronaut.validation:micronaut-validation-processor"

    compileOnly "io.grpc:grpc-stub"
    compileOnly "javax.annotation:javax.annotation-api"

    implementation "io.micronaut.validation:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation projects.micronautGrpcServerRuntime
    implementation "io.micronaut.discovery:micronaut-discovery-client"
    implementation "io.grpc:grpc-services"
    implementation "org.yaml:snakeyaml"

    runtimeOnly "ch.qos.logback:logback-classic"

    testAnnotationProcessor "io.micronaut.validation:micronaut-validation-processor"

    testCompileOnly "javax.annotation:javax.annotation-api"
}

// tag::config[]

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobufJavaVersion" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
// end::config[]
