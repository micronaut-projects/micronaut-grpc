plugins {
    id 'java'
    // tag::plugin[]
    id "com.google.protobuf" version "$protobufGradleVersion"
    // end::plugin[]
    id "io.micronaut.application" version "$micronautGradlePluginVersion"
}

repositories {
    mavenCentral()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        mavenContent {
            snapshotsOnly()
        }
    }
}

mainClassName = "helloworld.Application"
micronaut {
    version micronautVersion
    testRuntime "junit5"
    enableNativeImage false
    processing {
        incremental(true)
        annotations("helloworld.*")
    }
}

dependencies {

    annotationProcessor "io.micronaut:micronaut-validation"

    compileOnly "io.grpc:grpc-stub:$grpcVersion"
    compileOnly("javax.annotation:javax.annotation-api")

    implementation "io.micronaut:micronaut-validation"
    implementation("io.micronaut:micronaut-runtime")
    implementation(project(':grpc-server-runtime'))
    implementation "io.micronaut.discovery:micronaut-discovery-client"
    implementation "io.grpc:grpc-services"
    implementation("org.yaml:snakeyaml")

    runtimeOnly "ch.qos.logback:logback-classic"

    testAnnotationProcessor "io.micronaut:micronaut-validation"
}

// tag::config[]

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobufJavaVersion" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
// end::config[]
