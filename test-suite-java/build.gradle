plugins {
    id "java"
    // tag::plugin[]
    alias(libs.plugins.protobuf)
    // end::plugin[]
    id "io.micronaut.build.internal.grpc-tests"
}

// tag::variables[]
ext {
    grpcVersion = libs.versions.managed.grpc.asProvider().get()
    protobufVersion = libs.versions.managed.protobuf.asProvider().get()
}
// end::variables[]

mainClassName = "helloworld.Application"
micronaut {
    version.set(libs.versions.micronaut.platform.get())
    coreVersion.set(libs.versions.micronaut.asProvider().get())
    testRuntime "junit5"
    enableNativeImage false
    processing {
        incremental(true)
        annotations("helloworld.*")
    }
}

dependencies {

    annotationProcessor mnValidation.micronaut.validation.processor

    compileOnly libs.managed.grpc.stub
    compileOnly libs.javax.annotation.api

    implementation mnValidation.micronaut.validation
    implementation mn.micronaut.runtime
    implementation projects.micronautGrpcServerRuntime
    implementation libs.micronaut.discovery.client
    implementation libs.managed.grpc.services
    implementation mn.snakeyaml

    runtimeOnly mnLogging.logback.classic

    testAnnotationProcessor mnValidation.micronaut.validation.processor

    testCompileOnly libs.javax.annotation.api
    testRuntimeOnly mnLogging.logback.classic
    testCompileOnly libs.managed.protobuf.java

}

// tag::config[]

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobufVersion" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
// end::config[]
