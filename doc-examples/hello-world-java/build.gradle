plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.17'
    id 'io.micronaut.application' version '2.0.6'
}

ext {
    micronautVersion = '3.1.1'
    protobufJavaVersion = '3.17.2'
    grpcVersion = '1.39.0'
}

repositories {
    mavenCentral()
}

mainClassName = "helloworld.Application"
micronaut {
    version micronautVersion
    testRuntime "junit5"
    enableNativeImage false
}

dependencies {

    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"

    compileOnly "io.grpc:grpc-stub:$grpcVersion"
    compileOnly "io.micronaut:micronaut-inject-java"
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"

    implementation "io.micronaut.grpc:micronaut-grpc-server-runtime"

    implementation "io.micronaut.discovery:micronaut-discovery-client"
    runtimeOnly "ch.qos.logback:logback-classic"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testAnnotationProcessor "io.micronaut:micronaut-validation"

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "io.micronaut:micronaut-inject-java"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

// tag::config[]

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobufJavaVersion" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
// end::config[]
