plugins {
    id 'groovy'
    id "com.google.protobuf" version "$protobufGradleVersion"
    id "io.micronaut.minimal.application"
    id "io.micronaut.build.internal.grpc-base"
}

mainClassName = "helloworld.Application"
micronaut {
    version '4.0.0-SNAPSHOT'
    //version micronautVersion
    testRuntime "spock"
    enableNativeImage false
    processing {
        incremental(true)
        annotations("helloworld.*")
    }
}

dependencies {
    compileOnly "io.grpc:grpc-stub"
    compileOnly "javax.annotation:javax.annotation-api"
    compileOnly "io.micronaut.validation:micronaut-validation-processor"
    implementation "io.micronaut.validation:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut.grpc:micronaut-grpc-server-runtime"
    implementation "io.micronaut.discovery:micronaut-discovery-client"
    implementation "io.grpc:grpc-services"
    implementation "io.grpc:grpc-protobuf"
    implementation "com.google.protobuf:protobuf-java-util"
    implementation "org.yaml:snakeyaml"

    runtimeOnly "ch.qos.logback:logback-classic"

    testCompileOnly "javax.annotation:javax.annotation-api"

    testImplementation "org.apache.groovy:groovy"

    testRuntimeOnly "ch.qos.logback:logback-classic"
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
}

tasks.withType(org.gradle.jvm.tasks.Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

sourceSets {
    main {
        groovy {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobufJavaVersion" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
