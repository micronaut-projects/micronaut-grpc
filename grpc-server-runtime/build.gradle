plugins {
    id 'io.micronaut.build.internal.grpc-module'
    id 'com.google.protobuf'
}

repositories {
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    api project(":grpc-annotation")
    api project(":grpc-health")
    api project(":grpc-opentracing")
    api libs.micronaut.runtime
    api libs.managed.grpc.protobuf
    api libs.managed.grpc.stub

    compileOnly libs.managed.grpc.services
    implementation libs.grpc.netty
    implementation libs.micronaut.buffer.netty
    compileOnly libs.micronaut.discovery.client
    compileOnly libs.micronaut.management
    compileOnly libs.graal

    testImplementation libs.managed.grpc.services
    testImplementation libs.micronaut.discovery.client
    testImplementation libs.micronaut.reactor.http.client
    testImplementation libs.micronaut.management
    testImplementation project(":grpc-client-runtime")

    testRuntimeOnly libs.netty.tcnative
    testRuntimeOnly libs.netty.tcnative.boringssl.static
}

sourceSets {
    test {
        java {
            srcDirs 'build/generated/source/proto/test/grpc'
            srcDirs 'build/generated/source/proto/test/java'
        }
    }
}

protobuf {
    protoc { artifact = libs.protoc.asProvider().get() }
    plugins {
        grpc { artifact = libs.protoc.grpc.get() }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

micronautBuild {
    binaryCompatibility {
        enabled = true
        acceptedRegressionsFile = file("config/accepted-api-changes.json")
    }
}