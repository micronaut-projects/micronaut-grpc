plugins {
    id "org.jetbrains.kotlin.jvm" 
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.kotlin.plugin.allopen"
    id "io.micronaut.application" 
    id "com.google.protobuf" version "$protobufGradleVersion"
    id "io.micronaut.build.internal.grpc-base"
}

// tag::variables[]
ext {
    grpcKotlinVersion = "1.3.0"
}
// end::variables[]

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

mainClassName = "helloworld.Application"
micronaut {
    version '4.0.0-SNAPSHOT'
    //version micronautVersion
    testRuntime "junit5"
    enableNativeImage false
    processing {
        incremental(true)
        annotations("helloworld.*")
    }
}

dependencies {
    kapt "io.micronaut.validation:micronaut-validation-processor"
    implementation "io.micronaut.validation:micronaut-validation"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.0-Beta"
    implementation "io.micronaut:micronaut-runtime"
    implementation projects.micronautGrpcServerRuntime
    implementation "org.yaml:snakeyaml"


    // tag::dependencies[]
    implementation "io.grpc:grpc-kotlin-stub:$grpcKotlinVersion"
    implementation "io.grpc:grpc-services"
    compileOnly "io.grpc:grpc-stub"
    compileOnly "javax.annotation:javax.annotation-api"
    // end::dependencies[]

    runtimeOnly "ch.qos.logback:logback-classic"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin"

    testCompileOnly "javax.annotation:javax.annotation-api"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "17"
        javaParameters = true
    }
}

// tag::config[]

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/grpckt'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobufJavaVersion" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
        grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:${grpcKotlinVersion}:jdk8@jar" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            grpckt {}
        }
    }
}

// end::config[]
